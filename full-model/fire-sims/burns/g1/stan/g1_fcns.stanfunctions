  // custom distribution functions of EGPD
  real egpd_lpdf(real y, real sigma, real xi, real kappa) {
    if (kappa > 1e-15) {
      return log(kappa) + (kappa-1) * gpareto_lcdf(y | sigma, xi) + 
              gpareto_lpdf(y | sigma, xi);
    }
    else {
      reject("kappa<=0; found kappa = ", kappa);
    }
  }  
  real egpd_cdf(real y, real sigma, real xi, real kappa) {
    if (kappa > 1e-15) {
      return exp(kappa * gpareto_lcdf(y | sigma, xi));
    }
    else {
      reject("kappa<=0; found kappa = ", kappa);
    }
  }
  real egpd_lcdf(real y, real sigma, real xi, real kappa) {
    if (kappa > 1e-15) {
      return kappa * gpareto_lcdf(y | sigma, xi);
    }
    else {
      reject("kappa<=0; found kappa = ", kappa);
    }
  }  
  real egpd_lccdf(real y, real sigma, real xi, real kappa) {
    if (kappa > 1e-15) {
      return log1m_exp(kappa * gpareto_lcdf(y | sigma, xi));
    }
    else {
      reject("kappa<=0; found kappa = ", kappa);
    }
  }  
  real egpd_icdf(real u_adj, real sigma, real xi, real kappa) {
    // input is 'u' adjusted for lower bound truncation
    if (kappa > 1e-15) {
      real p = exp(exp(-log(kappa)) * log(u_adj));
      return gpareto_icdf(p, sigma, xi);
    }
    else {
      reject("kappa<=0; found kappa = ", kappa);
    }
  }  
  
  vector egpd_rng(int n, real ymin, real sigma, real xi, real kappa) {
    // returns a vector length n of burn area predictions
    real cst = egpd_cdf(ymin | sigma, xi, kappa);
    vector[n] rng_val;
    vector[n] alpha = rep_vector(0, n);
    vector[n] beta = rep_vector(1, n);
    array[n] real u = uniform_rng(alpha, beta);
    for (i in 1:n) {
      real u_adj = u[i] * (1-cst) + cst;
      rng_val[i] = egpd_icdf(u_adj, sigma, xi, kappa);
    }
    return rng_val;
  }
  // truncated EGPD distribution
  real egpd_trunc_lpdf(real y, real ymin, real sigma, real xi, real kappa) {
    real lpdf = egpd_lpdf(y | sigma, xi, kappa);
    real cst = egpd_lccdf(ymin | sigma, xi, kappa);
    return lpdf - cst;
  }
  
  // probability forecast
  vector prob_forecast(int n_int, vector int_pts, real ymin, real sigma, real xi, real kappa) {
    vector[n_int] pred_probs;
    real numer_cst = egpd_cdf(ymin | sigma, xi, kappa);
    real denom_cst = exp(egpd_lccdf(ymin | sigma, xi, kappa));
    for (n in 1:n_int) {
      real unnorm_pred_prob = egpd_cdf(int_pts[n] | sigma, xi, kappa);
      pred_probs[n] = (unnorm_pred_prob - numer_cst)/denom_cst;
    }
    return pred_probs;
  }  